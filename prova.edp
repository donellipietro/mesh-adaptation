verbosity = 0;
bool plots = true;

real[int] colorhsv=[ // color hsv model
   4./6., 1 , 0.5, // dark blue
   4./6., 1 , 1, // blue
   5./6., 1 , 1, // magenta
   1, 1. , 1 // red
];

// Data ----
// include "data/data_poisson.idp"
include "data/data_scenario_0.idp"
// include "data/data_scenario_1.idp"
// include "data/data_scenario_2.idp"

func f1 = exp(-1000*((x-0.5)^2 + (y-0.5)^2));


// Problem discretization ----

// Mesh
mesh Th = square(50, 50);
// plot(Th, wait = true);

// Functional spaces
fespace Vh(Th, P2);
Vh uh, vh;

// Diffusion tensor discretization
Vh d11h, d12h, d22h;
d11h = d11;
d12h = d12;
d22h = d22;

Vh f1h;
f1h = f1;

plot(f1h, value=true, fill=true, wait=true, hsv=colorhsv);

// Weak-formulation
problem Poisson(uh, vh, solver = CG, eps = 1e-6) = 
    int2d(Th)(
        d11h*dx(uh)*dx(vh) + 
        d12h*dx(uh)*dy(vh) + 
        d12h*dy(uh)*dx(vh) +
        d22h*dy(uh)*dy(vh) ) -
    int2d(Th)( f1*vh ) + 
    on(1,2,3, uh = 0) +
    on(4, uh = 0);

// Solution
Poisson;
plot(uh, fill = true, wait = true, dim = 2, hsv=colorhsv);