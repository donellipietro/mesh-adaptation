include "utils.idp"

verbosity = 0;
bool plots = false;

// Options ----

real TOL = 0.5;
int nitermax = 200;
real hmin = 1e-3;
real hmax = 1;
int nbvx = 1e6;


// Data ----

include "../data/data_scenario_1.idp"
// include "../data/data_scenario_2.idp"

// if(scenario == 1) hmin = 8*1e-3;
// if(scenario == 2) hmin = 5*1e-3;


// Problem discretization ----

// Mesh
mesh Th = square(20, 20);
if(plots) plot(Th, wait = true);

// Functional spaces
fespace Vh(Th, P2);
fespace VhP0(Th, P0);
fespace VhP1(Th, P1);

// Discretized diffusion tensor components
Vh logd11hStar, logd12hStar, logd22hStar;
VhP1 logd11h, logd12h, logd22h;

// errors
varf vsqetaK(unused, chiK) = 
    int2d(Th)(
        chiK*(
            square(dx(logd11hStar) - dx(logd11h)) + 2*square(dx(logd12hStar) - dx(logd12h)) + square(dx(logd22hStar) - dx(logd22h)) +
            square(dy(logd11hStar) - dy(logd11h)) + 2*square(dy(logd12hStar) - dy(logd12h)) + square(dy(logd22hStar) - dy(logd22h))
        )
    );


// Adaptation loop ----

// Loop initialization
int niter = 0;
real eta = 1e9;
real etamin = 1e9;
int etacounter = 0;
real TOLint = TOL;
int abovecounter = 0; 

// Adaptation loop
while(niter < nitermax && etacounter < 10) {

    // Update evaluation
    logd11hStar = logd11;
    logd12hStar = logd12;
    logd22hStar = logd22;
    logd11h = logd11;
    logd12h = logd12;
    logd22h = logd22;

    // Iteration initialization
    int Nh = Th.nt;

    // Compute etaK
    VhP0 sqetaK;
    sqetaK[] = vsqetaK(0, VhP0);
    if(plots) plot(sqetaK, cmm = "sqetaK " + niter,  wait=true, fill=true, value=true);

    // Check global error
    eta = sqrt(sqetaK[].sum);

    // Exit
    if(eta < TOL){
        if(eta < etamin){
            etamin = eta;
            etacounter = 0;
        } else {
            etacounter ++;
        }
    } else {
        abovecounter ++;
        if(abovecounter > 10) {
            TOLint = TOLint*0.9;
            abovecounter = 0;
        }
        if(eta < etamin){
            etamin = eta;
        }
    }

    // Info
    cout << "Iteration: " << niter << endl;
    cout << "TOL: " + TOL << endl;
    cout << "TOLint: " + TOLint << endl;
    cout << "above counter: " + abovecounter << endl;
    cout << "Nh: " << Nh << endl;
    cout << "etaK min: " << sqetaK[].min << endl;
    cout << "etaK max: " << sqetaK[].max << endl;
    cout << "eta: " + eta << endl;
    cout << "eta min: " + etamin << endl;
    cout << "eta counter: " + etacounter << endl;
    cout << endl;

    // Compute hK (new)
    
    VhP0 hKold = hTriangle;
    VhP0 A = area;
    VhP0 Aref = 1;
    VhP0 hKnew = pow((TOLint^2)/(Nh * Aref * (sqetaK/A)), 0.5);
    // plot(hK, cmm = "hK " + niter, wait=true, fill=true, value=true);

    // Averaging
    VhP1 hNnew;
    hNnew[] = averagingNodes(Th, hKnew[], hmin, hmax);
    if(plots) plot(hNnew, cmm = "hN " + niter, wait=true, fill=true, value=true);

    // Mesh adaptation
    Th = adaptmesh(Th, hNnew, IsMetric = true, nbvx = nbvx);
    plot(Th, cmm = "Th " + niter, wait = false);

    // Update iterator
    niter++;

}

plot(Th, cmm = "Final Th", wait=1);
Vh gammah = gamma;
plot(Th, gammah, cmm = "Final Th", wait=1);

cout << "Number of triangles: " << Th.nt << endl;

mesh ThHR = square(200, 200);
if(plots) plot(Th, wait = true);

fespace V(ThHR, P1);
V logd11hadapt, logd12hadapt, logd22hadapt;
V logd11htrue, logd12htrue, logd22htrue;

logd11hadapt = logd11h;
logd12hadapt = logd12h;
logd22hadapt = logd22h;

logd11htrue = logd11;
logd12htrue = logd12;
logd22htrue = logd22;

eta = sqrt(int2d(Th)(
    square(dx(logd11htrue) - dx(logd11hadapt)) + 2*square(dx(logd12htrue) - dx(logd12hadapt)) + square(dx(logd22htrue) - dx(logd22hadapt)) +
    square(dy(logd11htrue) - dy(logd11hadapt)) + 2*square(dy(logd12htrue) - dy(logd12hadapt)) + square(dy(logd22htrue) - dy(logd22hadapt))
));
cout << "Eta: " << eta << endl;