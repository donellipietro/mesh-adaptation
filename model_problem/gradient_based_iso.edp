include "utils.idp"

verbosity = 0;
bool plots = false;

// Options ----

real TOL = 0.05;
int nitermax = 200;
real hmin = 5*1e-3;
real hmax = 1;
int nbvx = 1e6;


// Data ----

// include "../data/data_poisson.idp"
include "../data/data_scenario_0.idp"
// include "../data/data_scenario_1.idp"
// include "../data/data_scenario_2.idp"


// Problem discretization ----

// Mesh
mesh Th = square(20, 20);
if(plots) plot(Th, wait = true);

// Functional spaces
fespace Vh(Th, P2);
fespace VhP1(Th, P1);
fespace VhP0(Th, P0);
Vh uh, vh;

// Diffusion tensor discretization
Vh d11h, d12h, d22h;

// Weak-formulation
problem Poisson(uh, vh, solver = CG, eps = 1e-6) = 
    int2d(Th)(
        d11h*dx(uh)*dx(vh) + 
        d12h*dx(uh)*dy(vh) + 
        d12h*dy(uh)*dx(vh) +
        d22h*dy(uh)*dy(vh) ) -
    int2d(Th)( f*vh ) + 
    on(1,2,3, uh = gD1) +
    on(4, uh = gD2);


// gradient norm
varf vsqetaK(unused, chiK) = int2d(Th)( chiK*(square(dx(uh)) + square(dy(uh))) );


// Adaptation loop ----

// Loop initialization
int niter = 0;
real etaKmean = 1e9;
real etaKmeanmin = 1e9;
int etacounter = 0;
real TOLint = TOL;
int abovecounter = 0; 

// Adaptation loop
while(niter < nitermax && etacounter < 15) {

    // Update evaluation
    int Nh = Th.nt;
    d11h = d11;
    d12h = d12;
    d22h = d22;
    Poisson;

    // Compute etaK
    VhP0 etaK;
    etaK[] = vsqetaK(0, VhP0);
    etaK = sqrt(etaK);
    if(plots) plot(etaK, cmm = "etaK " + niter,  wait=false, fill=true, value=true);

    // Exit
    etaKmean = etaK[].sum / etaK[].n;
    if(etaKmean < TOL){
        if(etaKmean < etaKmeanmin){
            etaKmeanmin = etaKmean;
            etacounter = 0;
        } else {
            etacounter ++;
        }
    } else {
        abovecounter ++;
        if(abovecounter > 10) {
            TOLint = TOLint*0.5;
            abovecounter = 0;
        }
        if(etaKmean < etaKmeanmin){
            etaKmeanmin = etaKmean;
        }
    }

    // Info
    VhP0 hK = hTriangle;
    cout << "Iteration: " << niter << endl;
    cout << "TOL: " + TOL << endl;
    cout << "TOLint: " + TOLint << endl;
    cout << "above counter: " + abovecounter << endl;
    cout << "Nh: " << Nh << endl;
    cout << "hmin: " << hK[].min << endl;
    cout << "hmax: " << hK[].max << endl;
    cout << "etaK min: " << etaK[].min << endl;
    cout << "etaK mean: " << etaKmean << endl;
    cout << "etaK max: " << etaK[].max << endl;
    cout << "etaK mean min: " << etaKmeanmin << endl;
    cout << "eta counter: " + etacounter << endl;
    cout << endl;

    // Compute hK (new)
    VhP0 AK = area;
    VhP0 Aref = 1;
    VhP0 hKnew = pow((TOLint^2)/(Aref * (square(etaK)/AK)), 0.5);
    if(plots) plot(hKnew, cmm = "hK " + niter, wait=true, fill=true, value=true);

    // Averaging
    VhP1 hNnew;
    hNnew[] = averagingNodes(Th, hKnew[], hmin, hmax);
    if(plots) plot(hNnew, cmm = "hN " + niter, wait=true, fill=true, value=true);

    // Mesh adaptation
    Th = adaptmesh(Th, hNnew, IsMetric = true, nbvx = nbvx);
    plot(Th, cmm = "Th " + niter, wait = false);

    // Update iterator
    niter++;

}

plot(Th, cmm = "Final Th", wait=1);
plot(Th, uh, cmm = "Final Th", wait=1);
plot(uh, cmm = "Final Th", wait=1, fill = true);

cout << "Number of triangles: " << Th.nt << endl;