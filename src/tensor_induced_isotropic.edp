// 
// tensor-induced metric based adaptation, isotropic field
//

include "utils.edp"

// Options ----
verbosity = 0;
bool store = false;    // whether to store final computed mesh
bool plots = false;    // whether to display intermediate plots
real hmax = 7.5e-2;    // maximum element size
real ratiomax = 5;     // maximum element aspect-ratio
int nbvx = 1e6;
real beta = 2.;
int maxiter = 100;     // maximum number of iterations
// ------------

func alpha = 1.;
func gamma = 1;
func nux   = 1.0;
func nuy   = 0.0;
// tensor components [d11, d12, d22]
func d11 = alpha * (nux^2 * gamma + nuy^2 / gamma);
func d12 = alpha * (nux * nuy * (gamma - 1. / gamma));
func d22 = alpha * (nuy^2 * gamma + nux^2 / gamma);

// build Delaunay mesh of unit square
mesh Th = square(100, 100);
if (plots) plot(Th, wait = true, cmm = "Starting point");

// Functional spaces
fespace VhP1(Th, P1); // piecewise linear elements
int n = VhP1.ndof;

real[int] r10V(n), r11V(n), r20V(n), r21V(n); // eigenvector matrix
real[int] ev0(n), ev1(n);                     // eigenvalues pair
real[int] alphaV(n);                          // eigenvalue geometric mean: \sqrt{ev0[i] * ev1[i]}
real[int] gammaV(n);                          // anisotropy ratio: ev0[i]/alphaV[i]
VhP1 d11N = d11;
VhP1 d12N = d12;
VhP1 d22N = d22;
real[int] d11V = d11N[];
real[int] d12V = d12N[];
real[int] d22V = d22N[];

decomposeTensorField(
   d11N[], d12N[], d22N[],           // inputs
   ev0, ev1, r10V, r11V, r20V, r21V  // outputs
);

for(int i = 0; i < n; ++i) {
   alphaV[i] = sqrt(ev0[i] * ev1[i]);
   gammaV[i] = ev0[i] / alphaV[i];
}

// start adaptation loop
for(int niter = 0; niter < maxiter; niter ++) {

    // build finite element representation
    fespace WhP1(Th, P1);
    WhP1 alphaN;
    WhP1 gammaN;
    WhP1 r10N, r11N, r20N, r21N;
    alphaN[] = alphaV;
    gammaN[] = gammaV;
    r10N[] = r10V;
    r11N[] = r11V;
    r20N[] = r20V;
    r21N[] = r21V;

    // build metric from tensor field as
    // M = R^\top * diag(1/(gamma^beta * alpha * hmax^2), gamma^beta / (alpha * hmax^2)) * R

    VhP1 gammabetaN = pow(gammaN, beta);                  // gamma^beta
    VhP1 sqlaK = alphaN * gammabetaN;                     // alpha * gamma^beta
    real hmaxtilde = hmax / sqrt(sqlaK[].max);            // hmax / sqrt{alpha * gamma^beta}
    VhP1 sqlambda1N = hmaxtilde^2 * alphaN * gammabetaN;  // 1./(gamma^beta * alpha * hmax^2)
    VhP1 sqlambda2N = hmaxtilde^2 * alphaN / gammabetaN;  // gamma^beta / (alpha * hmax^2)

    // constraint on elements aspect ratio
    for (int i = 0; i < WhP1.ndof; ++i) {
        real ratio = sqlambda1N[][i]/sqlambda2N[][i];
        if (ratio > ratiomax) {
           sqlambda1N[][i] = sqlambda2N[][i] * (ratiomax * ratiomax);
        }
    }

    // compute R^\top * diag(1/(gamma^beta * alpha * hmax^2), gamma^beta / (alpha * hmax^2)) * R
    VhP1 m11N = r10N^2 * 1./sqlambda1N + r20N^2 * 1./sqlambda2N;
    VhP1 m12N = r10N * r11N * 1./sqlambda1N + r20N * r21N * 1./sqlambda2N;
    VhP1 m22N = r11N^2 * 1./sqlambda1N + r21N^2 * 1./sqlambda2N;

    if (plots) {
       plot(m11N, wait = true, fill = true, cmm = "m11N");
       plot(m12N, wait = true, fill = true, cmm = "m12N");
       plot(m22N, wait = true, fill = true, cmm = "m22N");
    }

    // BAMG adaptation
    mesh ThAdapt = adaptmesh(Th, metric = [m11N[], m12N[], m22N[]], nbvx = nbvx); 
    if (plots) plot(Th, fill = false, wait = false);

    // reproject tensor on adapted mesh
    fespace WhP1Adapt(ThAdapt, P1);
    real[int] d11Old = d11V, d12Old = d12V, d22Old = d22V;
    matrix A = interpolate(WhP1Adapt, WhP1);
    real[int] d11VNew = A * d11Old;
    real[int] d12VNew = A * d12Old;
    real[int] d22VNew = A * d22Old;
    
    // reallocate memory for updated tensor decomposition
    int m = WhP1Adapt.ndof;
    d11V.resize(m); d12V.resize(m); d22V.resize(m);
    alphaV.resize(m); gammaV.resize(m);
    r10V.resize(m); r11V.resize(m); r20V.resize(m); r21V.resize(m);
    
    decomposeTensorField(
        d11VNew, d12VNew, d22VNew,             // inputs
        ev0, ev1, r10V, r11V, r20V, r21V // outputs
    );

    for(int i = 0; i < m; ++i) {
        alphaV[i] = sqrt(ev0[i] * ev1[i]);
        gammaV[i] = ev0[i] / alphaV[i];
    }

    // update tensor data and mesh
    d11V = d11VNew;
    d12V = d12VNew;
    d22V = d22VNew;
    Th = ThAdapt;
}

// final diagnostic
plot(Th, cmm = "Adapted mesh", fill = false, wait = true);
fespace WhP1(Th, P1);
WhP1 gammaFEM;
gammaFEM[] = gammaV;
plot(Th, gammaFEM, cmm = "Adapted mesh", fill = false, wait = true);
cout << "Number of triangles: " << Th.nt << endl;
cout << "Number of nodes: " << Th.nv << endl;

// save computed mesh
if (store) { savemesh(Th, "tensor_metric_"+hmax+".mesh"); }
